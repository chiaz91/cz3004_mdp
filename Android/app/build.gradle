import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "ntu.cz3004.controller"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 31
        versionName "1.9.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        SimpleDateFormat sdf = new SimpleDateFormat("dd/MM/yyyy HH:mm")
        buildConfigField "String", "TIMESTAMP", "\"" + sdf.format(System.currentTimeMillis()) + "\""
    }

    flavorDimensions "default"
    productFlavors{
        client{
            dimension "default"
        }
        full{
            dimension "default"
            applicationIdSuffix ".full"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = "mdp_control_v${variant.versionName}_${variant.name}.apk"
        }
    }

}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.preference:preference:1.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'


}